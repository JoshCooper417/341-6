# 1 "./tests/bsttest.oat"
# 1 "<built-in>"
# 1 "<command-line>"
# 1 "./tests/bsttest.oat"

# 1 "lib/bst.oat" 1



bool equals(Object? o1, Object? o2) {
  return o1 == o2;
}

class Node <: Object
{
  int val;
  Node? parent;
  Node? left;
  Node? right;
  new(int val)()
  {
    this.val=val;
    this.parent=null;
    this.left=null;
    this.right=null;
  }
};




class BST <: Object
{
  Node? root;

  new()()
  {
    this.root=null;
  }

  unit insert(int x)
  {
    Node? cur=this.root;
    Node? prev=null;
    int found=0;
    while (found == 0 & !equals(cur, null))
    {
      if?(Node cur_=cur)
        if (x==cur_.val)
        {
          found=1;
        }
        else if (x<cur_.val)
        {
          prev=cur;
          cur=cur_.left;
        }
        else
        {
          prev=cur;
          cur=cur_.right;
        }
    }
    if (found == 0) 
    {
      
      Node n=new Node(x);
      n.parent=prev;
      if?(Node prev_=prev)
        if (x<prev_.val)
          prev_.left=n;
        else
          prev_.right=n;
      else  
        this.root=n;
    }

    return;
  }

  
  int find(int x)
  {
    Node? cur=this.root;
    int found=0;
    while (found==0 & !equals(cur,null))
    {
      if?(Node cur_=cur)
        if (x==cur_.val)
        {
          found=1;
        }
        else if (x<cur_.val)
          cur=cur_.left;
        else
          cur=cur_.right;
    }
    return found;
  }

  
  unit remove(int x)
  {
    Node? cur=this.root;
    Node? prev=null;
    int found=0;
    while (found==0 & !equals(cur,null))
    {
      if?(Node cur_=cur)
        if (x==cur_.val)
        {
          found=1;
        }
        else if (x<cur_.val)
        {
          prev=cur;
          cur=cur_.left;
        }
        else
        {
          prev=cur;
          cur=cur_.right;
        }
    }
    if (found==0)
      fail ("Element not found");
    else
      if?(Node c=cur)
        this.remove_node(c);
      else
        fail ("Fatal Error: Inconsistent node state.");
    return;
  }

  unit remove_node(Node c)
  {
    if (equals(c.left,null) & equals(c.right,null))
      if?(Node p=c.parent)
        if (equals(p.left,c))
          p.left=null;
        else
          p.right=null;
      else 
        this.root=null;
    else if (equals(c.left,null))
      if?(Node p=c.parent)
        if (equals(p.left,c))
          p.left=c.right;
        else
          p.right=c.right;
      else 
        this.root=c.right;
    else if (equals(c.right,null))
      if?(Node p=c.parent)
        if (equals(p.left,c))
          p.left=c.left;
        else
          p.right=c.left;
      else 
        this.root=c.left;
    else  
    {
      
      if?(Node t=c.right)
      {
        while(!equals(t.left,null))
          if?(Node tt=t.left) t=tt; else fail ("Unexpected");
        
        
        c.val=t.val;

        
        this.remove_node(t);
      }
      else
        fail ("Unexpected");
    }
    return;
  }
};



# 2 "./tests/bsttest.oat" 2

BST t=new BST();

int program (int argc, string[] argv) {
  int r=0;
  t.insert(3);
  t.insert(1);
  t.insert(0);
  t.insert(5);
  t.insert(2);
  t.insert(4);
  {
    int x=t.find(0);
    t.remove(3);
    {
      int y=t.find(5);
      int z=t.find(3); 
      r=100*x+10*y+z;
    }
  }
  return r;
}


